describe 'asWord' [
  itShould 'noop if already word' [
    #hello asWord #hello = 
  ]

  itShould 'convert quote to word' [
    'hello' asWord #hello =
  ]

  itShould 'peel off all quoting' [
    ########hello asWord #hello =
  ]

  itDies 'when quote is empty' [
    '' asWord
  ]

  itDies 'when quote is whitespace' [
    ' ' asWord
  ]

  itDies 'when quote is newline + whitespace' [
    '


    ' asWord
  ]

  itDies 'when quote contains whitespace' [
    'hello world' asWord
  ]
]

describe 'uppercase?' [
  itShould 'return false for empty string' [
    '' uppercase? false =
  ]

  itShould 'return false for whitespace string' [
    [ ] [
      ' ' uppercase? false =
      '     ' uppercase? false =
      '
      ' uppercase? false = 
    ] there [ ] all?
  ]

  itShould 'work for single-letter quotes' [
    [ ] [
      'H' uppercase?  true =
      'Á' uppercase?  true =
      'c' uppercase? false =
      '.' uppercase? false =
    ] there [ ] all?
  ]

  itShould 'work for multi-letter quotes' [
    [ ] [
      'HELLO' uppercase?  true =
        'HÁË' uppercase?  true =
      'HeLLO' uppercase? false =
      'HÁËå'  uppercase? false =
    ] there [ ] all?
  ]
]
