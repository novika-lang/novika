describe 'asWord' [
  itShould 'noop if already word' [
    #hello asWord #hello = 
  ]

  itShould 'convert quote to word' [
    'hello' asWord #hello =
  ]

  itShould 'peel off all quoting' [
    ########hello asWord #hello =
  ]

  itDies 'when quote is empty' [
    '' asWord
  ]

  itDies 'when quote is whitespace' [
    ' ' asWord
  ]

  itDies 'when quote is newline + whitespace' [
    '


    ' asWord
  ]

  itDies 'when quote contains whitespace' [
    'hello world' asWord
  ]
]

describe 'uppercase?' [
  itShould 'leave false for empty quote' [
    '' uppercase? false =
  ]

  itShould 'leave false for whitespace quote' [
    [ ] [
      ' ' uppercase? false =
      '     ' uppercase? false =
      '
      ' uppercase? false = 
    ] there [ ] all?
  ]

  itShould 'work for single-letter quotes' [
    [ ] [
      'H' uppercase?  true =
      'Á' uppercase?  true =
      'c' uppercase? false =
      '.' uppercase? false =
    ] there [ ] all?
  ]

  itShould 'work for multi-letter quotes' [
    [ ] [
      'HELLO' uppercase?  true =
        'HÁË' uppercase?  true =
      'HeLLO' uppercase? false =
      'HÁËå'  uppercase? false =
    ] there [ ] all?
  ]
]

describe 'toUppercase' [
  itDies 'when not given a quote #1' [
    0 toUppercase
  ]

  itDies 'when not given a quote #2' [
    #foo toUppercase
  ]

  itShould 'leave empty quote unchanged' [
    '' toUppercase '' =
  ]

  itShould 'leave whitespace/single-uppercase-character quote unchanged' [
    [ ] [
      ' ' toUppercase ' ' =
      'C' toUppercase 'C' =
      'Ä' toUppercase 'Ä' =
      '.' toUppercase '.' =
      '0' toUppercase '0' =
      '°' toUppercase '°' =
      '€' toUppercase '€' =
    ] there [ ] all?
  ]


  itShould 'uppercase single lowercase character' [
    [ ] [
      'c' toUppercase 'C' =
      'ä' toUppercase 'Ä' =
      'ā' toUppercase 'Ā' =
    ] there [ ] all?
  ]

  itShould 'uppercase multi-character quotes' [
    [ ] [
      'HI' toUppercase 'HI' =
      'Hi' toUppercase 'HI' =
      'hI' toUppercase 'HI' =
      'Ḧï' toUppercase 'ḦÏ' =
      'Ãuxi£åÅ' toUppercase 'ÃUXI£ÅÅ' =
    ] there [ ] all?
  ]
]
