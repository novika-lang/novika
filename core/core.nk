"""This is the entry point of Novika core.

We need to quickly go from low-level and playground-ish to
high-level and serious. Novika core is all about that.
"""

"First, we take hold of the current block (the block associated
with this file), and call it `_root`. With an underscore,
because that's our root, not their root."

cont getContBlock dup #_root swap pushes


_root #there [
  "( S B -- S ): opens Block in Stack, leaves Stack. Ahead
   is transferred to block."
  <| dup |> ahead #hydrate inject
] opens


_root #@: [
  "( F @: N ): makes an opener entry called Name for Form."
  ahead swap ahead thru swap opens
] opens


[ "( F $: N -- ): makes a pusher entry called Name for Form."
  ahead swap ahead thru swap pushes
] @: $:


[ "( V =: N -- ): submits Value to an already defined entry
   under the specified Name."
  ahead swap ahead thru swap submit
] @: =:


[ "( help F -- ): echoes help for Form. If form is a word,
   fetches it in caller first."
  ahead dup thru dup word?
    [ entry:fetch ]
    [ nip         ]
  br desc echo
] @: help


[ "( F1 F2 -- ): echoes a pair of Forms."
  swap echo echo
] @: 2echo


[ "( F -- F ): echoes a Form but leaves it."
  dup echo
] @: p


[ "( F1 F2 -- F1 F2 ): echoes a pair of Forms but leaves them."
  2dup 2echo
] @: 2p


[ """( Q F -- ): dies with Quote message stitched with an
   enquoted Form.

  >>> 'expected foobar, got: ' 100 2die
  Sorry: expected foobar, got: 100.
  """
  toQuote stitch die
] @: 2die


[ "( B -- Mt ): leaves Monotonic time difference in milliseconds
   for Block (leaves the time Block took to execute, in ms)."
  monotonic $: t1
  do
  monotonic $: t2

  t2 t1 -
] @: measure


[ "( needs: Pq -- ): ensures that the user has enabled the package
   with id specified by Package quote."
  ahead thru toQuote $: packageId

  novika:packages [ packageId = ] any? not =>
    [
      244 67 54 rgb withEchoFg
        '! This program requires the `' packageId stitch '` package. Consult the frontend' stitch
        '  you are using for information on how to include it.'
        swap withColorEcho
             withColorEcho
      dropEchoFg

      'missing package: ' packageId stitch die
    ]
] @: needsPackage:
