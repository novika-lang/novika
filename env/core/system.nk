[ "( F -- ): enquotes Form, and appends the resulting quote, followed
   by newline, to the standard output stream."
  appendEcho '\n' appendEcho
] @: echo


[ """( say: F -- ): infix version of `echo`.

  ```
  say: 'Hello World'
  say: [ 1 2 + ]
  ```

  The above prints:

  ```text
  Hello World
  3
  ```
  """
  ahead thruVal echo
] @: say:


[ "( help F -- ): echoes help for Form. If form is a word, fetches
   it in caller first."
  ahead dup thru dup word?
    [ entry:fetch ]
    [ nip         ]
  br desc echo
] @: help


[ "( F1 F2 -- ): echoes a pair of Forms."
  swap echo echo
] @: 2echo


[ "( F -- F ): echoes a Form but leaves it on the stack."
  dup echo
] @: p


[ "( F1 F2 -- F1 F2 ): echoes a pair of Forms but leaves them on
   the stack."
  2dup 2echo
] @: 2p


[ "( Pf -- Aq ): same as `readLine`, but Answer quote is left even
   if the user rejected the prompt (in such case it is empty)."
  readLine or: ''
] @: ask


[ """( ask: Pf -- Aq ): infix version of `ask`.

  ```
  ask: 'What is your name? ' $: name
  say: [ ['Nice to meet you, ' name '!'] ~* ]
  ```
  """
  ahead thruVal ask
] @: ask:


[ "( B -- Mt ): leaves Monotonic time difference in milliseconds
   for Block (leaves the time Block took to execute, in ms)."
  monotonic $: t1
  do
  monotonic $: t2

  t2 t1 -
] @: measure


[ "( -- ): ends the program with the exit code 1 (an error)."
  1 bye
] @: okbye
