[ ] $: block

100 times: [ 1 + block gulp  ]

block echo

'How many elements are in the block?' echo
block count echo

'What is the sum of elements in the block?' echo
block sum echo

'What is the product of elements in the block (100! =)' echo
block [ * ] 1 reduce echo

'What is the min, max elements in the block?' echo
block min echo
block max echo

'How many even numbers?' echo
block # [ even? ] echo

'How many odd numbers?' echo
block # [ odd? ] echo

'Sums of pairs:' echo

block [
  'Sum of' echo
  2p
  '... is:' echo
  + echo
] pairs

1 $: consProduct

'c = a * b * c where a, b are cons pairs in block (just a big number):' echo
block [ * consProduct * =: consProduct ] consPairs

consProduct echo

[ $: y
  $: x

  [
    [ ] x toQuote <<  ' @ ' << y toQuote <<
    [ stitch ] '' reduce
  ] @: __quote__

  this
] @: point

"These should be nicely printed:"
100 200 point echo
300 400 point echo

[
  new $: extenderInstance $: extendee

  "Run extenderInstance in itself."
  extenderInstance 0 |to
  [ ] extenderInstance newContinuation conts gulp

  "Make extender instance's parent = extendee's parent."
  extenderInstance extendee parent reparent
  extendee adopt
] @: extend

[ x y + Algebraic stuff test huh? ] $: block

block new orphan reparent [
  "Extend with a word trap that does nothing."
  [ ] @: __trap__
] extend

vals echo "should be: [ x y + Algebraic stuff test huh? ]"

stack echo "should be [ ]"
